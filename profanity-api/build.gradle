plugins {
    id "org.asciidoctor.jvm.convert" version "3.3.2"
}


bootJar.enabled = true
jar.enabled = false

ext {
    snippetsDir = file('build/generated-snippets') as File
}

configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    runtimeOnly(project(':profanity-storage:redis'))
    runtimeOnly(project(':profanity-storage:rdb'))

    implementation project(':profanity-core')
    implementation project(':profanity-domain')

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

tasks.register("prepareKotlinBuildScriptModel") {}

test {
    useJUnitPlatform() {
        excludeTags 'restdocs'
    }
}

tasks.register('restdocs', Test) {
    outputs.dir snippetsDir
    useJUnitPlatform {
        includeTags 'restdocs'
    }
}

tasks.register('copyRestDocs', Copy) {
    dependsOn asciidoctor
    from file("build/docs/asciidoc/index.html")
    into '../docs'
}

tasks.register('generateRestDocs') {
    dependsOn restdocs, asciidoctor, copyRestDocs
}
asciidoctor {
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    dependsOn restdocs
    sourceDir file('src/docs')
    sources {
        include("index.adoc")
    }
    baseDirFollowsSourceFile()
}
